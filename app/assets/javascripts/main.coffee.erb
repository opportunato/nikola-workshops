$ ->
  $window = $(window)
  $body = $("body")
  $kazarma = $(".kazarma")
  $header = $("header")
  $menuButton = $(".menuButton")
  $workshopList = $(".workshopList")
  $overlay = $(".overlay")
  $intro = $(".intro")
  TILDE_CODE = 192

  if (window.location.pathname == "/")
    title = document.title

  $(document).on 'keydown', (e) ->
    if e.keyCode == TILDE_CODE
      e.preventDefault()

      if id = $(".workshop-wrapper").data('id')
        document.location = "/admin/workshops/#{id}/edit"
      else
        document.location = '<%= admin_path %>'

  initWorkshop = (id) ->
    $carousel = $(".carousel")

    if $carousel.length > 0
      carousel = new Carousel(".carousel")
      carousel.init()

      arrowLeft = $(".arrowLeft")
      arrowRight = $(".arrowRight")

      previousCarousel = ->
        carousel.prev()

      nextCarousel = ->
        carousel.next()

      Hammer(arrowRight[0]).on "tap", (event) ->
        nextCarousel()

      Hammer(arrowLeft[0]).on "tap", (event) ->
        previousCarousel()

      $window.keydown (event) ->

        if event.keyCode == 37
          previousCarousel()
        else if event.keyCode == 39
          nextCarousel()

      adaptCarousel()

      addFacebookButton()
      addVKButton()

      addVKGroup(id)

      $(".carousel-image").responsive_images 
        mobile_size: 640,
        tablet_size: 1024,
        desktop_size: 1600

  closeWorkshop = ->
    $overlay.removeClass 'opened'
    $body.removeClass 'noscroll'

    setTimeout ->
      if !$overlay.hasClass('opened')
        $overlay.html("")
    , 2000

  openWorkshop = (id, title) ->
    $overlay.addClass 'opened'
    $body.addClass 'noscroll'
    document.title = title

    $.get "/workshops/#{id}.js",
      (data) ->
        $overlay.html(data)
        initWorkshop(id)

      , 'html'

  addFacebookButton = ->
    try FB.XFBML.parse()
    catch e

  addVKButton = ->
    VK.Widgets.Like("vk-like", { type: "mini", pageUrl: window.location.href });

  addVKGroup = (id) ->
    VK.Widgets.Group("vk-groups-#{id}", {mode: 0, width: "300", height: "241", color1: 'FFFFFF', color2: '2B587A', color3: '5B7FA6'}, 73689861);

  adaptKazarma = ->

  adaptCarousel = ->
    vpw = $window.width()
    vph = $window.height()
    $carousel = $(".carousel")

    $kazarma.height vph

    if vpw > 1024
      $carousel.height vph
    else if vpw < 1024
      $carousel.height 540
    else
      $carousel.height 360

  adaptCarousel()

  if id = $(".workshop-wrapper").data('id')
    initWorkshop(id)


  if !$.cookie('preloaded')
    typedTimeout = 3200
  else
    typedTimeout = 1600

  # typed.js
  setTimeout ->
    $("#typed").typed
      strings: ["Воркшопы с международными специалистами в уникальной среде Никола-Ленивца."]
      typeSpeed: 10
  , typedTimeout

  $window.resize ->
    adaptCarousel()

  # TODO с timeout какой-то грязный хак, не нравится мне это

  $menuButton.on 'click', ->
    $body.toggleClass 'noscroll'

    setTimeout ->
      $header.toggleClass 'opened'
    , 50

  $overlay.on 'click', '.menuButtonPopup', ->
    history.pushState({}, "", "/")

    document.title = title

    closeWorkshop()

  $workshopList.on 'click', ".workshopWrapper", (e) ->
    e.preventDefault()
    id = $(e.currentTarget).data('id')
    link = $(e.currentTarget).prop('href')
    title = $(e.currentTarget).find('.title').text()

    openWorkshop(id, title)

    history.pushState({workshopId: id, title: title}, "", link)

  $window.on "popstate", (e) ->
    state = e.originalEvent.state || {}

    if (state.workshopId)
      openWorkshop(state.workshopId, state.title)
    else if (window.location.pathname == "/")
      closeWorkshop()

    document.title = state.title || title
